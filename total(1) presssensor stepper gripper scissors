#include <Servo.h>
#define SENSOR 7
//int KEY_NUM = 0;
int count = 0;
int state = 0;
int pos = 0;
Servo myservo;
int servoPin = 4;

int relayPin_1 = 3;
int relayPin_2 = 2;

int stepperDirectionPin = 5;
int stepperSpeedPin = 6;


//--------------------------------------夹爪
void initialGripper() {
  myservo.attach(servoPin, 500, 2500);
}

void undo() {//松开
  for (pos = 42; pos <= 120; pos += 2) {
    myservo.write(pos);
    delay(15);					
  }
  delay(500);
}

void grip() {//抓紧
  for (pos = 120; pos >= 42; pos -= 2) {
    myservo.write(pos);
    delay(15); 					
  }
  delay(500);
}
//--------------------------------------剪刀（继电器）
void setRelays() {
  pinMode(relayPin_1, OUTPUT);
  pinMode(relayPin_2, OUTPUT);
  digitalWrite(relayPin_1, LOW);
  digitalWrite(relayPin_2, LOW);
}

void switchKnife() {
  digitalWrite(relayPin_1, HIGH);
  delay(1000);
  digitalWrite(relayPin_1, LOW);
  delay(1000);
}

void initialKnife() {
  setRelays();
  switchKnife();
  digitalWrite(relayPin_2, HIGH);
  delay(100);
  digitalWrite(relayPin_2, LOW);
  delay(100);
  digitalWrite(relayPin_2, HIGH);
  delay(100);
  digitalWrite(relayPin_2, LOW);
  delay(100);
  digitalWrite(relayPin_2, HIGH);
  delay(100);
  digitalWrite(relayPin_2, LOW);
  delay(1000);
}

void cut() {
  digitalWrite(relayPin_2, HIGH);
  delay(500);
  digitalWrite(relayPin_2, LOW);
  delay(500);
}

void closeKnife() {
  digitalWrite(relayPin_2, HIGH);
  delay(5000);
  digitalWrite(relayPin_2, LOW);
  switchKnife();
}
//-------------------------------------------步进电机
void initialStepper() {
  pinMode(stepperDirectionPin,OUTPUT);//设置引脚5为方向引脚
  pinMode(stepperSpeedPin,OUTPUT);//设置引脚6为发射脉冲引脚
}
void moveForward(int n) {
  Serial.println("F");
  //设定转动方向
  digitalWrite(stepperDirectionPin, LOW);
  //发送脉冲信号

  for (int i = 0; i <= n; i++) 
  {
    digitalWrite(stepperSpeedPin, HIGH);
    delayMicroseconds(100); //设置高电平脉冲持续时间
    digitalWrite(stepperSpeedPin, LOW);
    delayMicroseconds(100); //设置低电平脉冲间隔时间
    
  }
}
void moveBackward(int n) {
  Serial.println("B");
  //设定转动方向
  digitalWrite(stepperDirectionPin, HIGH);
  //发送脉冲信号

  for (int i = 0; i <= n; i++) 
  {
    digitalWrite(stepperSpeedPin, HIGH);
    delayMicroseconds(100); //设置高电平脉冲持续时间
    digitalWrite(stepperSpeedPin, LOW);
    delayMicroseconds(100); //设置低电平脉冲间隔时间
    
  }
}
//--------------------------------------------压力传感器
void initialPSensor() {
  pinMode(SENSOR,INPUT);
}

int scanSensor()
{
  while(digitalRead(SENSOR) == LOW);
  return(1);
}
//-----------------------------------------------
void setup() {
  // put your setup code here, to run once:
  Serial.begin(9600);
  initialGripper();
  initialKnife();
  initialStepper();
  initialPSensor();
}

void loop() {
  // put your main code here, to run repeatedly:
  scanSensor();
  Serial.println("success!");
  grip();
  moveForward(4000);
  cut();
  moveBackward(7000);
  undo();
  moveForward(3000);
}
